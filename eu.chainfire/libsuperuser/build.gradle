apply plugin: 'com.android.library'

android {
    compileSdkVersion 31
    buildToolsVersion '32.0.0'

    defaultConfig {
        minSdkVersion 5
        targetSdkVersion 31
    }

    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src']
            resources.srcDirs = ['src']
            aidl.srcDirs = ['src']
            renderscript.srcDirs = ['src']
            res.srcDirs = ['res']
            assets.srcDirs = ['assets']
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_6
    }
}

version = "1.1.0." + (new Date()).format('yyyyMMddHHmm')
group = "eu.chainfire"

dependencies {
    implementation 'androidx.annotation:annotation:1.3.0'
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))

    android.libraryVariants.all { variant ->
        variant.javaCompileProvider.configure {
            javadoc.classpath += files(it.classpath.files)
        }
    }
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
    archives sourcesJar
}