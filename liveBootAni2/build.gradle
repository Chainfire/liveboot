apply plugin: 'com.android.application'

android {
    compileSdkVersion 31
    buildToolsVersion '30.0.3'
    defaultConfig {
        applicationId "eu.chainfire.liveboot"
        minSdkVersion 31
        targetSdkVersion 31
        versionCode 184
        versionName '1.84'
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    signingConfigs {
        debug {
//            storeFile file(System.properties['keystoreDebug'])
        }
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-project.txt'
        }
        debug {
            signingConfig signingConfigs.debug
        }
    }
    applicationVariants.all { variant ->
        variant.outputs.all { output ->
            if (variant.buildType.name == 'release') {
                outputFileName = "LiveBoot" + "-v" + android.defaultConfig.versionName + "-" + (new Date()).format('yyyyMMddHHmmss') + ".apk"
                variant.getAssembleProvider().configure() {
                    it.doLast {
                        copy {
                            from 'build/outputs/mapping/release'
                            into 'proguard'
                            include '**/mapping.txt'
                        }
                    }
                }
            }
        }
    }
}

dependencies {
    implementation 'androidx.legacy:legacy-support-core-utils:1.0.0'
    implementation 'androidx.annotation:annotation:1.3.0'
    implementation project(path: ':eu.chainfire:librootjava')
    implementation project(path: ':eu.chainfire:librootjavadaemon')
    implementation project(path: ':eu.chainfire:libcfsurface')
    implementation project(path: ':eu.chainfire:libsuperuser')
}
